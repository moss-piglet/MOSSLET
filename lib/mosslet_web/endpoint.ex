defmodule MossletWeb.Endpoint do
  use Sentry.PlugCapture
  use Phoenix.Endpoint, otp_app: :mosslet

  # Enable concurrent testing for Wallaby
  if sandbox = Application.compile_env(:mosslet, :sandbox, false) do
    plug Phoenix.Ecto.SQL.Sandbox, sandbox: sandbox
  end

  # The session will be stored in the cookie and signed,
  # this means its contents can be read but not tampered with.
  # Set :encryption_salt if you would also like to encrypt it.
  @session_options [
    store: :cookie,
    key: "_mosslet_key",
    signing_salt: {Mosslet.Encrypted.Session, :signing_salt, []},
    encryption_salt: {Mosslet.Encrypted.Session, :encryption_salt, []},
    same_site: "Lax"
  ]

  # We pass the `:user_agent` in the websocket for Wallaby testing
  # We also pass `:peer_data` and `:x_headers` for IP-based bot defense at socket level
  socket "/live", Phoenix.LiveView.Socket,
    websocket: [connect_info: [:peer_data, :x_headers, :user_agent, session: @session_options]],
    longpoll: [connect_info: [:peer_data, :x_headers, session: @session_options]]

  # Serve at "/" the static files from "priv/static" directory.
  #
  # When code reloading is disabled (e.g., in production),
  # the `gzip` option is enabled to serve compressed
  # static files generated by running `phx.digest`.
  plug Plug.Static,
    at: "/",
    from: :mosslet,
    gzip: not code_reloading?,
    only: MossletWeb.static_paths()

  # Tidewave ai support
  if Code.ensure_loaded?(Tidewave) do
    plug Tidewave
  end

  # Code reloading can be explicitly enabled under the
  # :code_reloader configuration of your endpoint.
  if code_reloading? do
    socket "/phoenix/live_reload/socket", Phoenix.LiveReloader.Socket
    plug Phoenix.LiveReloader
    plug Phoenix.CodeReloader
    plug Phoenix.Ecto.CheckRepoStatus, otp_app: :mosslet
  end

  plug Phoenix.LiveDashboard.RequestLogger,
    param_key: "request_logger",
    cookie_key: "request_logger"

  plug Plug.RequestId
  plug Plug.Telemetry, event_prefix: [:phoenix, :endpoint]

  plug Stripe.WebhookPlug,
    at: "/webhooks/stripe",
    handler: Mosslet.Billing.Providers.Stripe.WebhookHandler,
    secret: {Application, :get_env, [:stripity_stripe, :signing_secret]}

  plug Plug.Parsers,
    parsers: [:urlencoded, :multipart, :json],
    pass: ["*/*"],
    json_decoder: Phoenix.json_library(),
    # 8 MB by trix.js calculations
    length: 8_388_608

  plug Sentry.PlugContext

  plug MossletWeb.Plugs.ContentSecurityPolicy

  plug Plug.MethodOverride
  plug Plug.Head
  plug Plug.Session, @session_options

  plug RemoteIp, headers: ~w[fly-client-ip]

  plug :canonical_host

  plug MossletWeb.Plugs.BotDefense

  plug MossletWeb.Router

  defp canonical_host(conn, _opts) do
    canonical = Application.get_env(:mosslet, :canonical_host)
    request_host = get_request_host(conn)

    cond do
      conn.request_path in ["/health", "/api/health"] ->
        conn

      is_nil(canonical) or canonical == "" ->
        conn

      request_host == canonical ->
        conn

      true ->
        location = build_canonical_url(conn, canonical)

        conn
        |> Plug.Conn.put_resp_header("location", location)
        |> Plug.Conn.send_resp(301, "Moved Permanently")
        |> Plug.Conn.halt()
    end
  end

  defp get_request_host(conn) do
    conn.host
  end

  defp build_canonical_url(conn, canonical_host) do
    scheme =
      case Plug.Conn.get_req_header(conn, "x-forwarded-proto") do
        [proto | _] ->
          proto

        [] ->
          if Application.get_env(:mosslet, :env) == :prod,
            do: "https",
            else: to_string(conn.scheme)
      end

    query =
      case conn.query_string do
        "" -> ""
        qs -> "?" <> qs
      end

    "#{scheme}://#{canonical_host}#{conn.request_path}#{query}"
  end
end
